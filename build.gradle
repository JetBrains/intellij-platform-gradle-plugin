plugins {
    id 'com.gradle.plugin-publish' version '0.9.4'
    id 'synapticloop.documentr' version '1.3.4'
    id 'java-gradle-plugin'
    id 'maven'
    id 'signing'
}

apply plugin: 'groovy'

sourceCompatibility = 1.7
targetCompatibility = 1.7
plugins.withType(JavaPlugin) {
    project.tasks.withType(GroovyCompile) { task ->
        task.sourceCompatibility = project.sourceCompatibility
        task.targetCompatibility = project.targetCompatibility
    }
}

repositories {
    maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
    maven { url mavenCentralUrl }
}

dependencies {
    compile localGroovy()
    compile gradleApi()
    compile 'org.jetbrains:annotations:13.0'
    compile 'org.jetbrains.intellij:plugin-repository-rest-client:0.3.16'
    compile 'org.jetbrains.intellij.plugins:intellij-plugin-structure:1.23'

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4', {
        exclude module: 'groovy-all'
    }
    testCompile 'junit:junit:4.12'
}

version = '0.2.0-SNAPSHOT'
group = 'org.jetbrains.intellij.plugins'
description = """
This plugin allows you to build plugins for IntelliJ platform using specific IntelliJ SDK and bundled plugins.

The plugin adds extra IntelliJ-specific dependencies, patches processResources tasks to fill some tags 
(name, version) in `plugin.xml` with appropriate values, patches compile tasks to instrument code with 
nullability assertions and forms classes made with IntelliJ GUI Designer and provides some build steps which might be
helpful while developing plugins for IntelliJ platform.
"""

pluginBundle {
    website = 'https://jetbrains.com/'
    vcsUrl = 'https://github.com/JetBrains/gradle-intellij-plugin'
    description = 'Plugin for building plugins for IntelliJ IDEs'
    tags = ['intellij', 'jetbrains', 'idea']

    //noinspection GroovyAssignabilityCheck
    plugins {
        intellijPlugin {
            id = 'org.jetbrains.intellij'
            displayName = 'Gradle IntelliJ Plugin'
        }
    }
}

test {
    doFirst {
        new File("$buildDir/localRepo").mkdir()
        new File("$buildDir/testGradleHome").mkdir()
    }
    systemProperties['local.repo'] = "$buildDir/localRepo"
    systemProperties['test.gradle.home'] = "$buildDir/testGradleHome"
    dependsOn install
}

install {
    repositories {
        mavenDeployer {
            repository(url: new File("$buildDir/localRepo").toURI())
        }
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                artifactId project.name
                name 'Gradle IntelliJ Plugin'
                description project.description
                url 'https://github.com/JetBrains/gradle-intellij-plugin'
                packaging 'jar'

                scm {
                    connection 'scm:git:https://github.com/JetBrains/gradle-intellij-plugin/'
                    developerConnection 'scm:git:https://github.com/JetBrains/gradle-intellij-plugin/'
                    url 'https://github.com/JetBrains/gradle-intellij-plugin/'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'zolotov'
                        name 'Alexander Zolotov'
                        email 'zolotov@jetbrains.com'
                    }
                }
            }
        }
    }
}