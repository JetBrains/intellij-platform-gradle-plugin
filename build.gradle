plugins {
    id "com.gradle.plugin-publish" version "0.9.1"
    id 'maven'
}

apply plugin: 'groovy'

compileGroovy {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {
    maven { url 'http://repo.labs.intellij.net/central-proxy' }
    maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.jetbrains:annotations:13.0'

    compile 'org.jetbrains.intellij:plugin-repository-rest-client:0.3.16'
    compile 'org.jetbrains.intellij.plugins:intellij-plugin-structure:1.14'
    compile 'org.jetbrains.intellij.plugins:common-utils:1.18'
    
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4', {
        exclude module: 'groovy-all'
    }
}

version = '0.1.0'
group = 'org.jetbrains'

pluginBundle {
    website = 'https://jetbrains.com/'
    vcsUrl = 'https://github.com/JetBrains/gradle-intellij-plugin'
    description = 'Plugin for building plugins for IntelliJ IDEs'
    tags = ['intellij', 'jetbrains', 'idea']

    //noinspection GroovyAssignabilityCheck
    plugins {
        intellijPlugin {
            id = 'org.jetbrains.intellij'
            displayName = 'Gradle IntelliJ Plugin'
        }
    }
}

test {
    doFirst {
        new File("$buildDir/localRepo").mkdir()
        new File("$buildDir/testGradleHome").mkdir()
    }
    systemProperties['local.repo'] = "$buildDir/localRepo"
    systemProperties['test.gradle.home'] = "$buildDir/testGradleHome"
    dependsOn install
}

install {
    repositories {
        mavenDeployer {
            repository(url: new File("$buildDir/localRepo").toURI())
        }
    }
}