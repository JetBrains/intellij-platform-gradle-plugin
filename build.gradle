plugins {
    id 'com.gradle.plugin-publish' version '0.10.0'
    id 'synapticloop.documentr' version '2.9.0'
    id 'java-gradle-plugin'
    id 'maven'
}

apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'

sourceCompatibility = 1.7
targetCompatibility = 1.7
plugins.withType(JavaPlugin) {
    project.tasks.withType(GroovyCompile) { task ->
        task.sourceCompatibility = project.sourceCompatibility
        task.targetCompatibility = project.targetCompatibility
    }
}

repositories {
    maven { url 'https://cache-redirector.jetbrains.com/jetbrains.bintray.com/intellij-plugin-service' }
    maven { url 'https://cache-redirector.jetbrains.com/repo1.maven.org/maven2' }
    maven { url 'https://cache-redirector.jetbrains.com/jcenter.bintray.com' }
}

dependencies {
    compile localGroovy()
    compile gradleApi()
    compile 'org.jetbrains:annotations:13.0'
    compile 'org.jetbrains.intellij:plugin-repository-rest-client:0.4.30'
    compile 'org.jetbrains.intellij.plugins:structure-base:3.46'
    compile 'org.jetbrains.intellij.plugins:structure-intellij:3.46'
    compile 'de.undercouch:gradle-download-task:3.4.3'

    testCompile gradleTestKit()
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4', {
        exclude module: 'groovy-all'
    }
    testCompile 'junit:junit:4.12'
}

version = '0.4.2'
group = 'org.jetbrains.intellij.plugins'
description = """
**This project requires Gradle 3.4 or newer**

This plugin allows you to build plugins for IntelliJ platform using specific IntelliJ SDK and bundled plugins.

The plugin adds extra IntelliJ-specific dependencies, patches processResources tasks to fill some tags 
(name, version) in `plugin.xml` with appropriate values, patches compile tasks to instrument code with 
nullability assertions and forms classes made with IntelliJ GUI Designer and provides some build steps which might be
helpful while developing plugins for IntelliJ platform.
"""

pluginBundle {
    website = 'https://github.com/JetBrains/gradle-intellij-plugin'
    vcsUrl = 'https://github.com/JetBrains/gradle-intellij-plugin'
    description = 'Plugin for building plugins for IntelliJ IDEs'
    tags = ['intellij', 'jetbrains', 'idea']

    plugins {
        intellijPlugin {
            id = 'org.jetbrains.intellij'
            displayName = 'Gradle IntelliJ Plugin'
        }
    }
}

task cacheIntolerantTest(type: Test) {
    configureTests(it)
    include '**/DownloadIntelliJSpec.class'
    filter.failOnNoMatchingTests = false
}

test {
    configureTests(it)
    exclude '**/DownloadIntelliJSpec.class'
    dependsOn cacheIntolerantTest
}

def configureTests(testTask) {
    def testGradleHomePath = "$buildDir/testGradleHome"
    testTask.doFirst {
        new File(testGradleHomePath).mkdir()
    }
    testTask.systemProperties['test.gradle.home'] = testGradleHomePath
    testTask.systemProperties['plugins.repo'] = pluginsRepo
    testTask.outputs.dir(testGradleHomePath)
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                artifactId project.name
                name 'Gradle IntelliJ Plugin'
                description project.description
                version "$snapshotVersion-SNAPSHOT"
                url 'https://github.com/JetBrains/gradle-intellij-plugin'
                packaging 'jar'

                scm {
                    connection 'scm:git:https://github.com/JetBrains/gradle-intellij-plugin/'
                    developerConnection 'scm:git:https://github.com/JetBrains/gradle-intellij-plugin/'
                    url 'https://github.com/JetBrains/gradle-intellij-plugin/'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'zolotov'
                        name 'Alexander Zolotov'
                        email 'zolotov@jetbrains.com'
                    }
                }
            }
        }
    }
}

wrapper {
    gradleVersion = '4.10'
    distributionUrl = "https://cache-redirector.jetbrains.com/services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}








