plugins {
    id "com.gradle.plugin-publish" version "0.9.1"
    id 'maven'
}

apply plugin: 'groovy'

compileGroovy {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

repositories {
    maven { url 'http://repo.labs.intellij.net/central-proxy' }
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.jetbrains:annotations:13.0'
    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    compile 'org.apache.httpcomponents:httpmime:4.5.1'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.3', {
        exclude module: 'groovy-all'
    }
}

version = '0.0.33'
group = 'org.jetbrains'

pluginBundle {
    website = 'https://jetbrains.com/'
    vcsUrl = 'https://github.com/JetBrains/gradle-intellij-plugin'
    description = 'Plugin for building plugins for IntelliJ IDEs'
    tags = ['intellij', 'jetbrains', 'idea']

    //noinspection GroovyAssignabilityCheck
    plugins {
        intellijPlugin {
            id = 'org.jetbrains.intellij'
            displayName = 'Gradle IntelliJ Plugin'
        }
    }
}

test {
    systemProperties['local.repo'] = "$buildDir/localRepo"
    systemProperties['test.gradle.home'] = "$buildDir/testGradleHome"
    dependsOn install
}

install {
    repositories {
        mavenDeployer {
            repository(url: new File("$buildDir/localRepo").toURI())
        }
    }
}